plugins {
    id 'scala'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '2.0.2'
    id "com.diffplug.gradle.spotless" version "3.13.0"
}

group = 'pro.civitaspo'
version = '0.0.8'

def digdagVersion = '0.9.31'
def scalaSemanticVersion = "2.12.6"
def depScalaVersion = "2.12"

repositories {
    mavenCentral()
    jcenter()
    maven {
        url 'https://dl.bintray.com/digdag/maven'
    }
}

dependencies {
    compile group: 'io.digdag', name: 'digdag-spi', version: digdagVersion
    compile group: 'io.digdag', name: 'digdag-plugin-utils', version: digdagVersion

    // https://mvnrepository.com/artifact/org.scala-lang/scala-library
    compile group: 'org.scala-lang', name: 'scala-library', version: scalaSemanticVersion

    ['ecs', 's3', 'sts'].each { svc ->
        // https://mvnrepository.com/artifact/com.amazonaws/
        compile group: 'com.amazonaws', name: "aws-java-sdk-${svc}", version: '1.11.451'
    }
}

shadowJar {
    classifier = null
    dependencies {
        exclude(dependency('io.digdag:.*'))
        exclude(dependency('.*:jackson.*:.*'))
    }
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
        }
    }
    repositories {
        maven {
            url "$buildDir/repo"
        }
    }
}

spotless {
    scala {
        scalafmt('1.5.1').configFile('.scalafmt.conf')
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileScala.options.encoding = 'UTF-8'
compileTestScala.options.encoding = 'UTF-8'
compileScala.options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"

